 uri "http://sadl.org/CyberReq02GSN.sadl" alias CyberReq02GSN.
 
 import "http://sadl.org/gsn.sadl".
 import "http://sadl.org/DeliveryDrone.sadl".

 GSNDeliveryDrone is a GSN with rootGoal G1.1.2.
  //start of CyberReq02 subtree
 G1.1.2 (note "The drone's resilience to malicious commands shall have integrity concern less severe than Hazardous") is a Goal, //(CyberReq02)
  	with inContextOf (an ExplanatoryContext C1 with reference (a Drone) with description "A package delivery drone"),
 	with inContextOf (a ExplanatoryContext C6 with reference (a Concern) with description "A concern affecting the resilience to malicious commands"),
 	has claim (a Claim with claimStatement
 					(a TriplePattern with gpSubject (the Drone),		
 									 with gpPredicate hasConcern,
 									 with gpObject (the Concern)),
 					with claimStatement
 					(a TriplePattern with gpSubject (the Concern),
 									 with gpPredicate ofType,
 									 with gpObject resMaliciousCommands),
 					with claimStatement
 					(a TriplePattern with gpSubject (the Concern),
 									 with gpPredicate ofClass,
 									 with gpObject Integrity), 									  									 
 					with claimStatement
 					(a TriplePattern with gpSubject (the Concern),
 									 with gpPredicate ofSeverity,
 									 with gpObject (a Severity)),
 					with claimStatement
 					(a TriplePattern with gpSubject (the Severity),
 									 with gpPredicate lessSevere,
 									 with gpObject HAZARDOUS)
 	           ).

 S1.1.2 (note "Argument: By G1.1.2.1, The delivery mechanism does not have any integrity concern") is a Strategy,
      with inContextOf (an ExplanatoryContext C6 
     	                  with reference (an InformationObject) with description "The resilience to malicious commands has a hazardous integrity concern if the  delivery mechanism has an integrity concern."
     ).
 
 G1.1.2 supportedBy S1.1.2 .

 S1.1.2 supportedBy G1.1.2.1 .
  
 G1.1.2.1 (note "The delivery mechanism shall not have any integrity concern") is a Goal, 
 	with inContextOf (a ReferentialContext C1), //Referencing the drone with the context C1 defined previously
 	with inContextOf (a ReferentialContext C4 with reference (a DeliveryItemMechanism) with description "The deliveryItemMechanism subsystem of a drone"),
 	has claim (a Claim with claimStatement
 					(a TriplePattern with gpSubject (the Drone),		
 									 with gpPredicate hasDIMechanism,
 									 with gpObject (the DeliveryItemMechanism)),
 					with claimStatement
 					(a TriplePattern with gpSubject (the DeliveryItemMechanism),
 									 with gpPredicate hasIConcern,
 									 with gpObject false)
 	           ).

 S1.1.2.1 (note "Argument: The probabilities are confirmed by attack/defense tree analysis performed by Soteria++") is a Strategy,
   with inContextOf (a ReferentialContext C9 
     	                  with location "https://github.com/ge-high-assurance/assurance-case/tree/master/DeliveryDrone/context/CASE_Consolidated_Properties.aadl" with description "CASE Consolidated properties.").
 
 G1.1.2.1 supportedBy S1.1.2.1 .
 
 E2 (note "Soteria++ output of probability of the minimum cutset") is a Solution,
 	with evidence (an EvidenceReference with location "https://github.com/ge-high-assurance/assurance-case/tree/master/DeliveryDrone/evidence/ImplProperties.xml").

 S1.1.2.1 supportedBy E2 .
 //end of CyberReq02 subtree
